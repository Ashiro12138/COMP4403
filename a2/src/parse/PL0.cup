package parse;

import java_cup.runtime.*;   // Library needed for Java-CUP for parsing
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java.util.List;
import java.util.ArrayList;
import java.util.Map;
import java.util.HashMap;

import source.*;
import tree.DeclNode;
import tree.ConstExp;
import tree.StatementNode;
import tree.ExpNode;
import tree.Operator;
import syms.*;

/** PL0 definition for CUP LALR(1) parser generator.
 * This module is responsible for parsing the input file in terms
 * of the tokens supplied by the lexical analyser (see PL0.flex),
 * and generating a symbol table skeleton and an abstract syntax 
 * tree for program.
 
 * Java-CUP will output a file "CUPParser.java" that contains the parser
 * for PL0. It also outputs CUPToken.java.
 * The name "CUPParser.java" is determined by the "parser" option and
 * the name "CUPToken.java" by the "symbols" option to the java_cup command 
 * line or to the corresponding ant task.
 */

parser code 
{:  /* This section provides some methods used by Java_CUP during parsing.
       They override its default methods for reporting syntax errors. */
        
    /** Retrieve the error handler to handle error messages. */
    private Errors errors = ErrorHandler.getErrorHandler();
    
    /** Override the default CUP syntax_error method with one 
     * that integrates better with the compiler's error reporting. */
    @Override
    public void syntax_error(Symbol cur_token) {
        errors.error("PL0 syntax error", ((ComplexSymbol) cur_token).xleft);
    }
    /** Override the default CUP unrecovered_syntax_error method with one 
     * that integrates better with the compiler's error reporting. */
    @Override
    public void unrecovered_syntax_error(Symbol cur_token) {
        errors.error("PL0 unrecovered syntax error", 
                        ((ComplexSymbol) cur_token).xleft);
    }
            
:}

action code 
{:  /* This section provides global variables and methods used in the
     * semantics actions associated with parsing rules.
     * These are the only global variables you should need. */

    /** Error handler for reporting error messages. */
    private Errors errors = ErrorHandler.getErrorHandler();
    
    /** The current symbol table scope is available globally in the static checker.
     *  Its current scope corresponds to the procedure/main program 
     *  being processed.
     */
    private Scope currentScope;

:}


/*--------------------------------------------------------------*
 *--------------------- TERMINAL SYMBOLS -----------------------*
 *--------------------------------------------------------------*
 * CUP generates a file CUPToken.java containing the Java definitions 
 * generated by from the following list of terminal symbols.
 * CUPToken.java is used to communicate with the lexical analyser. 
 * The name "CUPToken.java" is determined by the "symbols" option to java_cup
 * (specified in command line invoking java-cup or in the corresponding ant 
 * task).
 * See PL0.flex for the detailed definitions of the lexical tokens.
 */
terminal
    LPAREN,       /* ( */
    RPAREN,       /* ) */
    SEMICOLON,    /* ; */
    COLON,        /* : */
    ASSIGN,       /* := */
    PLUS,         /* + */
    MINUS,        /* - */
    TIMES,        /* * */
    DIVIDE,       /* / */
    EQUALS,       /* = */
    NEQUALS,      /* != */
    LEQUALS,      /* <= */
    GEQUALS,      /* >= */
    LESS,         /* < */
    GREATER,      /* > */
    RANGE,        /* .. */
    PERIOD,       /* . */
    COMMA,        /* , */
    POINTER,      /* ^ */
    LCURLY,       /* { */
    RCURLY,       /* } */
    LBRACKET,     /* [ */
    RBRACKET,     /* ] */
    KW_BEGIN,     /* begin */
    KW_CALL,      /* call */
    KW_CONST,     /* const */
    KW_DO,        /* do */
    KW_ELSE,      /* else */
    KW_END,       /* end */
    KW_IF,        /* if */
    KW_NEW,       /* new */
    KW_PROCEDURE, /* procedure */
    KW_READ,      /* read */
    KW_RECORD,    /* record */
    KW_THEN,      /* then */
    KW_TYPE,      /* type */
    KW_VAR,       /* var */
    KW_WHILE,     /* while */
    KW_WRITE,     /* write */
    ILLEGAL;      /* illegal symbol - not used in grammar */

/** The following terminal symbols have an attribute associated with them.
 */
terminal String  IDENTIFIER;   /* identifier */
terminal Integer NUMBER;       /* number */

/**-------------------------------------------------------------*
 *------------------ NONTERMINAL SYMBOLS -----------------------*
 *--------------------------------------------------------------*
 * Many nonterminals (but not all) have an attribute type associated 
 * with them. This must be a Java class (subclass of Object, e.g., 
 * Integer rather than int). Generic types are allowed.
 * This gives the type of the special attribute RESULT in the grammar 
 * rules in which the nonterminal appears on the left side of a grammar
 * rule, and the type of the attribute corresponding to the label 
 * attached to occurrences of the nonterminal on the right side of a rule.
 *
 * The attributes are used to construct the symbol table for the
 * declaration parts and the abstract syntax tree for the bodies of
 * procedures and the main program.
 */
non terminal DeclNode.ProcedureNode Program;
non terminal StatementNode.BlockNode   Block;
non terminal DeclNode.DeclListNode DeclarationList;
non terminal                  Declaration;
non terminal                  ConstDefSeq, 
                              ConstDef;
non terminal ConstExp         Constant;
non terminal                  TypeDefSeq, 
                              TypeDef;
non terminal Type             Type,
                              TypeIdentifier,
                              SubrangeType,
                              RecordType,
                              PointerType;
non terminal                  VarDeclSeq, 
                              VarDecl;
non terminal DeclNode.ProcedureNode  ProcedureDef;
non terminal SymEntry.ProcedureEntry ProcedureHead;
non terminal 
                              FormalParamList;
non terminal 
                              ActualParamList;
non terminal StatementNode.ListNode StatementList;
non terminal StatementNode    Statement,
                              CompoundStatement;
non terminal ExpNode          Condition,
                              RelCondition,
                              Exp,
                              Term,
                              Factor;
non terminal Operator         Relation,
                              AddOp,
                              MulOp,
                              UnaryOperator;
non terminal ExpNode          LValue;
non terminal Type.RecordType  Fields;
non terminal Type.Field       Field;
non terminal ExpNode.ExpListNode ExpList;

/**-------------------------------------------------------------*
 *------------------------ GRAMMAR RULES -----------------------*
 *--------------------------------------------------------------*
 * Grammar productions have actions associated with them, which are
 * executed after recognising (part of) the right side of the production.
 *  
 * The action is Java code contained between '{:' and ':}'
 * It is allowed to reference the values of attributes of symbols on 
 * the right side (the name of these attributes is the name after the 
 * ':' following a symbol, e.g., in the rule for Program the attribute 
 * associated with Block is 'block'), and assignments to the attribute of 
 * the left side nonterminal (which is always referred to as 'RESULT').
 *
 * Be careful to get the syntax of the '{:' and ':}' correct and to
 * make sure they match up, otherwise the error messages get very
 * confusing. Also don't forget the ";" at the end of a production.
 * 
 * To allow the locations of errors in the input source program to be
 * reported, each attribute, such as "id" associated with IDENTIFIER
 * in the production for ConstDef, has two further attributes "idxleft" 
 * and "idxright", representing the locations (think of a location as
 * a line and column number) of the leftmost and rightmost characters 
 * of the construct in the input source program.
 * Note that the name "idxleft" is constructed from the name "id" by
 * adding "xleft" to it. 
 *
 * Java-CUP has a special non-terminal symbol 'error', which is used to 
 * allow recovery from syntax errors in the program. 
 *
 * The action code usually comes at the end of the right side of a production,
 * however, it is possible to have multiple actions, each of which is executed
 * as soon as the symbols preceding it are matched.
 * For example, for Program the first thing on the right side is an 
 * action, and hence this action is executed before the right side of
 * program (namely Block) is recognised, i.e., before any of the 
 * Program is recognised. The second action following Block is executed
 * after the whole of the Block representing the program is recognised.
 */
Program ::=  /* empty */ 
        {:
            /* This action occurs before the whole program is recognised.
             * Construct initial symbol table with current scope the 
             * predefined scope. */
            //System.out.println("NO_LOCATION = " + ErrorHandler.NO_LOCATION);
            SymbolTable symtab = new SymbolTable();
            currentScope = symtab.getPredefinedScope();
            /* Set up a dummy symbol table entry for the main program */
            SymEntry.ProcedureEntry procMain = 
                currentScope.addProcedure("<main>", ErrorHandler.NO_LOCATION);
            if(procMain  == null) {
                errors.fatal("Could not add main program to symbol table",
                        ErrorHandler.NO_LOCATION);
                }
            /* Enter the scope for the main program and save the new local
             * scope in main's symbol table entry */
            currentScope = currentScope.newScope(procMain);
        :}
     Block:block
        {: 
            /* Returned result of the whole parsing phase */
            RESULT = new DeclNode.ProcedureNode(currentScope.getOwnerEntry(), block);
            /* This action is executed after the whole program 
             * has been recognised */
            currentScope = currentScope.getParent();
        :}
    | error
        {:
            /* A null result from Program will cause the compiler to
               avoid further processing. */
            RESULT = null;
        :}
    ;
Block ::= DeclarationList:dl CompoundStatement:b
        {: 
            //System.out.println("BLOCKXLEFT = " + bxleft + "," + bxright);
            RESULT = new StatementNode.BlockNode(bxleft, dl, b, currentScope);
        :}
    ;
DeclarationList ::= DeclarationList:dl ProcedureDef:p 
        {:
            dl.addDeclaration(p);
            RESULT = dl;
        :}
    | DeclarationList:dl Declaration
        {: 
            RESULT = dl;
        :}
    | /* empty */
        {:
            RESULT = new DeclNode.DeclListNode();
        :}
    ;

Declaration ::= KW_CONST ConstDefSeq
    |  KW_TYPE TypeDefSeq
    |  KW_VAR  VarDeclSeq
    ;

ConstDefSeq ::= ConstDef
    |  ConstDefSeq ConstDef
    ;

ConstDef ::= IDENTIFIER:id EQUALS Constant:c SEMICOLON
        {: 
            /* The attribute idxleft represents the location of the start
             * character of the IDENTIFIER token in the input stream.
             * addConstant returns null if id is already defined 
             * in the current scope */
            if(currentScope.addConstant(id, idxleft, c) == null) {
                errors.error("Constant identifier " + id + " already declared in this scope",
                        idxleft);
            }
        :}
    |  error 
    ;
/* The rules for Constant construct a (mini) abstract syntax tree 
 * for constant expressions (not to be confused with ExpNodes).
 */
Constant ::= NUMBER:n
        {: 
            RESULT = new ConstExp.NumberNode(nxleft, currentScope, 
                                              Predefined.INTEGER_TYPE, n); 
        :}
    |  MINUS:op Constant:c
        {: 
            RESULT = new ConstExp.NegateNode(opxleft, currentScope, c); 
        :}
    |  IDENTIFIER:id
        {: 
            RESULT = new ConstExp.ConstIdNode(idxleft, currentScope, id);
        :}
    |  error:err
        {:
            RESULT = new ConstExp.ErrorNode(errxleft, currentScope);
        :}
    ;

TypeDefSeq ::= TypeDef 
    |  TypeDefSeq TypeDef
    ;

TypeDef ::= IDENTIFIER:id EQUALS Type:type SEMICOLON
        {: 
            SymEntry.TypeEntry entry = currentScope.addType(id, idxleft, type);
            if(entry == null) {
                errors.error(id + " already defined", idxleft);
            }
        :}
    |  error 
    ;

Field ::= IDENTIFIER:id COLON TypeIdentifier:typeId
        {:
            /* Written to define a field and its type
             * this is a type definition and so it returns a type
             * Returns: Type.Field
             * e.g. x:int */
            RESULT = new Type.Field(idxleft, id, typeId);
        :}
    ;

Fields ::= Field:field
        {:
            /* Written to define multiple fields and their types. This makes it a type definition
             * and so it returns a type (and not an ExpNode)
             * Type.RecordType can hold a list of fields (id, typeId) so we can use that
             * Returns: Type.RecordType
             * e.g. x:int, y:boolean */
            RESULT = new Type.RecordType();

            // add field of type Field with id and typeId already parsed
            RESULT.add(field);
        :}
    |   Fields:fields SEMICOLON Field:field
        {:
            // Fields would already be declared as type RecordType

            // Valid type identifiers checked in Type.java
            // Recursive definitions checked in Type.java
            // Need to check that the names of the fields are distinct
            if (fields.containsField(field.getId())) {
                errors.error(field.getId() + " already declared", fieldxleft);
            } else {
                fields.add(field);
            }
            RESULT = fields;
        :}
     ;

RecordType ::= KW_RECORD Fields:fields KW_END
        {:
            /* Written to define a type of record with given fields (List of (id, typeId)) and
             * field types. This grammar isn't actually creating an instance of a record, it just
             * defines what fields this kind of record will hold. Note that when Fields is
             * parsed, it returns a RecordType with the fields already parsed, so we just want to
             * return that
             * Returns: Type.RecordType
             * e.g. record x:int, next:List end */

            RESULT = fields;
        :}
     ;
PointerType ::= POINTER TypeIdentifier:identifier
        {:
            /* This is written to define a pointer type. This is not an instance of a
             * pointer, it just states that it is a pointer ot a particular type. TypeIdentifier
             * is used because the type might not be known at parse time.
             *
             * Returns: Type.PointerType
             * e.g. ^ Node where Node is of Type.RecordType */

            RESULT = new Type.PointerType(identifier);
        :}
     ;

SubrangeType ::= LBRACKET:subr Constant:lo RANGE Constant:hi RBRACKET
        {:
            RESULT = new Type.SubrangeType(subrxleft, lo, hi);
        :}
     ;
Type ::= TypeIdentifier:type
        {: 
            RESULT = type;

        :}
    |   SubrangeType:type
        {:
            RESULT = type;
        :}
    |   RecordType:type
        {:
            RESULT = type;
        :}
    |   PointerType:type
        {:
            RESULT = type;
        :}
    |   error:err
        {:
            RESULT = Type.ERROR_TYPE;
        :}
    ;
TypeIdentifier ::= IDENTIFIER:id
        {: /* As the type identifier may not be defined at this point
            * the abstract syntax tree records the id, as well as the 
            * symbol table scope to look it up within later.
            */
            RESULT = new Type.IdRefType(idxleft, id, currentScope);
        :}
    ;
VarDeclSeq ::= VarDecl
    |  VarDeclSeq VarDecl
    ;

VarDecl ::= IDENTIFIER:id COLON TypeIdentifier:type SEMICOLON
        {: 
            // Variables are always of ReferenceType.
            Type.ReferenceType varType = new Type.ReferenceType(type);
            if(currentScope.addVariable(id, idxleft, varType) == null) {
                errors.error(id + " already declared", idxleft);
            }
        :}
    |  error
    ;
    
ProcedureDef ::= ProcedureHead:procEntry EQUALS Block:b SEMICOLON
        {:
            /* Executed after recognising the whole procedure */
            currentScope = currentScope.getParent();
            RESULT = new DeclNode.ProcedureNode(procEntry, b);
        :}
    ;

ProcedureHead ::= KW_PROCEDURE IDENTIFIER:id
            LPAREN FormalParamList:pl 
            RPAREN
        {: 
            SymEntry.ProcedureEntry procEntry;
            /* Add an entry for the procedure to the current scope */
            procEntry = currentScope.addProcedure(id, idxleft);
            if(procEntry == null) {
                errors.error(id + " already declared", idxleft);
                /* Create a dummy symbol table entry.
                 * Note that this entry isn't in symtab. */
                procEntry = new SymEntry.ProcedureEntry(id, idxleft);
                procEntry.setScope(currentScope);
            }
            /* Create a new scope for the symbols local to
             * the procedure. */
            currentScope = currentScope.newScope(procEntry);
            RESULT = procEntry;
        :}
    |  KW_PROCEDURE IDENTIFIER:id error
        {: 
            SymEntry.ProcedureEntry procEntry = 
                new SymEntry.ProcedureEntry(id, idxleft);
            procEntry.setScope(currentScope);
            currentScope = currentScope.newScope(procEntry);
            RESULT = procEntry;
        :}
    ;


/* To allow for the addition of procedure parameters. */
FormalParamList ::= /* empty */
    ;

CompoundStatement ::= KW_BEGIN StatementList:sl KW_END
        {:
            RESULT = sl;
        :}
    ;

StatementList ::= Statement:s
        {:
            RESULT = new StatementNode.ListNode(sxleft);
            RESULT.addStatement(s);
        :}
    |  StatementList:sl SEMICOLON Statement:s
        {:
            sl.addStatement(s);
            RESULT = sl;
        :}
    ;

Statement ::= KW_WHILE Condition:c KW_DO Statement:s
        {: 
            RESULT = new StatementNode.WhileNode(cxleft, c, s);
        :}
    |  KW_IF Condition:c KW_THEN Statement:s1 KW_ELSE Statement:s2
        {: 
            RESULT = new StatementNode.IfNode(cxleft, c, s1, s2);
        :}
    |  CompoundStatement:s
        {: 
            RESULT = s; 
        :}
    | KW_READ:r LValue:lval
        {: 
            RESULT = new StatementNode.AssignmentNode(lvalxleft, 
                     lval, new ExpNode.ReadNode(rxleft));
        :}
    |  KW_WRITE Exp:e
        {: 
            RESULT = new StatementNode.WriteNode(exleft, e);
        :}
    | LValue:lval ASSIGN Condition:rval
       {:
          RESULT = new StatementNode.AssignmentNode(lvalxleft, lval, rval);
       :} 
    |  KW_CALL IDENTIFIER:id LPAREN ActualParamList:pl RPAREN
        {: 
            RESULT = new StatementNode.CallNode(idxleft, id);
        :}
    |  error:loc
        {: 
            RESULT = new StatementNode.ErrorNode(locxleft);
        :}
    ;


ActualParamList ::= /* empty */
    ;

LValue ::= IDENTIFIER:id
        {:
            /* At this stage the identifier could be either a constant identifier or 
             * a variable identifier but which cannot be determined until static 
             * checking when the IdentifierNode will be transformed into either
             * a ConstNode or a VariableNode.
             */
            RESULT = new ExpNode.IdentifierNode(idxleft, id);
        :}
    | LValue:lval PERIOD IDENTIFIER:id
        {:
            /* This is written to access a field inside a record. The record should have already
             * been initialised by now with its RecordType. But we can't access that (going
             * backwards in parsing) so we can make a separate RecordReferenceNode to record what
             * we are trying to access, so we can look it up in the symbol table. Note that lvalue
             * could be ConstNode or VariableNode.
             * Returns: ExpNode.RecordFieldAccessNode
             * e.g. if r is a record, r.x */

            RESULT = new ExpNode.RecordFieldAccessNode(idxleft, lval, id);
        :}
    | LValue:lval POINTER
        {:
            /* The purpose of this grammar is to dereference a pointer to access the data
             * that the pointer points to. lval is the object we want to dereference. This could be
             * an identifier or a chain of record.field identifiers. lval^ will then be the value
             * pointed to
             * Returns: ExpNode.PointerDereferenceNode
             * e.g. a^ */

            RESULT = new ExpNode.PointerDereferenceNode(lvalxleft, lval);
        :}
    | error:e
        {:
            RESULT = new ExpNode.ErrorNode(exleft);
        :}
    ;

/* To allow for adding logical expressions. */
Condition ::= RelCondition:e
        {: 
            RESULT = e;
        :}
    ;
/* Relational operators are lower precedence than arithmetic operators. */
RelCondition ::= Exp:e 
        {:
            RESULT = e;
        :}
    |  Exp:e1 Relation:op Exp:e2
        {:
            RESULT = new ExpNode.BinaryNode(opxleft, op, e1,e2);
        :} 
    ;

Relation ::= EQUALS
        {:
            RESULT = Operator.EQUALS_OP;
        :}
    |  NEQUALS
        {:
            RESULT = Operator.NEQUALS_OP;
        :}
    |  LEQUALS
        {:
            RESULT = Operator.LEQUALS_OP;
        :}
    |  LESS
        {:
            RESULT = Operator.LESS_OP;
        :}
    |  GREATER
        {:
            RESULT = Operator.GREATER_OP;
        :}
    |  GEQUALS
        {:
            RESULT = Operator.GEQUALS_OP;
        :}
    ;
/* Expressions are structured into Exp, Term, and Factor to encode the
 * precedence of the operators in the grammar.
 */
Exp ::= Term:t
        {:
            RESULT = t;
        :}
    |  Exp:e1 AddOp:op Term:e2
        {:
            RESULT = new ExpNode.BinaryNode(opxleft, op, e1,e2);
        :}
    ;

AddOp ::= PLUS
        {:
            RESULT = Operator.ADD_OP;
        :}
    |  MINUS
        {:
            RESULT = Operator.SUB_OP;
        :}
    ;

Term ::= Factor:f
        {:
            RESULT = f;
        :}
    |  Term:e1 MulOp:op Factor:e2
        {:
            RESULT = new ExpNode.BinaryNode(opxleft, op, e1,e2);
        :}
    ;

MulOp ::= TIMES
        {:
            RESULT = Operator.MUL_OP;
        :}
    |  DIVIDE
        {:
            RESULT = Operator.DIV_OP;
        :}
    ;

Factor ::= PLUS Factor:e
        {:
            RESULT = e; 
        :}
    |  UnaryOperator:op Factor:e
        {:
            RESULT = new ExpNode.UnaryNode(opxleft, op, e);
        :}
    |  LPAREN Condition:c RPAREN
        {:
            RESULT = c;
        :}
    |  NUMBER:n
        {:
            RESULT = new ExpNode.ConstNode(nxleft, 
                    Predefined.INTEGER_TYPE, n.intValue());
        :}
    |  LValue:lval
        {:
            RESULT = lval;
        :}
    |   KW_NEW:newkeyword TypeIdentifier:type
        {:
            /* Written to create a pointer to a dynamically allocated object of type "type". May
             * need to set aside stack frames in code gen later. For now, we want to record the
             * creation of this object and its type.
             * Returns: ExpNode.NewPointerNode
             * e.g. "new List in p := new List */
            RESULT = new ExpNode.NewPointerNode(newkeywordxleft, type);
        :}
    |   TypeIdentifier:type LCURLY ExpList:list RCURLY
        {:
            /* Written to create a new record instance and the assignment of its fields. Its
             * fields depends on its type. We want to save the type of the record, and the values
             * given to its fields.
             * Returns: ExpNode.NewRecordNode
             * e.g. Node{2, p} where Node is an already defined RecordType */

            RESULT = new ExpNode.NewRecordNode(typexleft, type, list);
        :}
    ;
ExpList ::= Condition:condition
        {:
            /* One value */
            RESULT = new ExpNode.ExpListNode(conditionxleft);
            RESULT.add(condition);
        :}
    | ExpList:list COMMA Condition:condition
        {:
            /* ExpList holds a list of conditions which are BinaryNodes that represent expressions.
             * This ExpList is used to store the field values of a record in the order that they
             * should be assigned (e.g. [2, p]).
             */
            list.add(condition);
            RESULT = list;
        :}
    ;
UnaryOperator ::= MINUS
        {:
            RESULT = Operator.NEG_OP;
        :}
    ;
